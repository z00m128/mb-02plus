;====================================
;EPROM/EEPROM/FLASH booter for MB-02+
;      with fixed DMA sequence
;    by z00m^TCG^SinDiKAT 09/2018
;====================================

		OUTPUT "mb02boot.rom"

version:	equ	$01

;MB02 PORTS
;----------

FDC:		equ	$0F
DMA:		equ	$0B
MOT:		equ	$13
REZ:		equ	$33
FDS:		equ	$4F
FDD:		equ	$6F

;other definitions
;-----------------

ULA		equ	$fe
BootSector	equ	$8000

;Main program
;------------

		org	$0000

Start:		di
		ld	a,$D0		; FDC command - ForceInterrupt
		out	(FDC),a		; reset FDC
		ld	a,$C3		; DMA command - R6: Reset
		out	(DMA),a		; reset DMA
		xor	a
		ld	i,a
		out	(MOT),a		; stop all floppy motors and unselect any floppy drive

		out	($fe),a		; border 0

		ld	hl,$4000	; clear screen
		ld	de,$4001
		ld 	bc,$1aff
		ld	(hl),l
		ldir

		ld	hl,$5800
		ld	de,$5801
		ld	bc,$2ff
		ld	(hl),%00000111	; paper 0, ink 7
		ldir

		ld	sp,$7fff	; stack to RAM

Boot:		ld	e,%00000100
		ld	a,$D0		; FDC command - ForceInterrupt
		out	(FDC),a		; reset FDC
		ld	(BootSector),a	; clear BSDOS boot sector ID byte
		ld	a,$C3		; DMA command - R6: Reset
		out	(DMA),a		; reset DMA

HDorDD:		ld	a,0		; default if HD
		xor	1		; toggle HDD/DD
		ld	(HDorDD+1),a
		out	(REZ),a		; set HD or DD
		ld	a,3		; select drive0 and start motor0
		out	(MOT),a
		out	(FDC),a		; FDC command - track0

FloppyReady:	in	a,(MOT)		; is floppy drive ready?
		and	e
		jr	z,FloppyReady
		in	a,(FDC)		; FDC command - read byte
		and	e
		jr	z,Boot		; if head head is not on Track0 try again

		ld	a,1
		out	(FDS),a		; FDC set sector reg to 1
					;
		ld	bc,$100B	; DMA sequence: $10 bytes, port address $0b
		ld	hl, DMASequence
		otir			; initialise Z80DMA
					;
		ld	a,88h
		out	(FDC),a		; FDC command - read sector

FloppyReady2:	in	a,(MOT)		; is floppy drive ready?
		and	e
		jr	z,FloppyReady2
		in	a,(FDC)		; FDC command - read byte
		and	$1C
		jr	nz,Boot		; try again if some read error occured
		ld	a,(BootSector)
		cp	18h		; check	BSDOS boot sector ID byte
		jr	nz,Boot		; try again if it's wrong

BootEnd:	ld	a,0D0h		; FDC command - ForceInterrupt
		out	(FDC),a		; reset FDC
		ld	a,$C3		; DMA command - WR6: Reset
		out	(DMA),a		; reset DMA
		xor	a
		out	(MOT),a		; stop all motors and unselect any floppy drive
		ld	a,$3F
		ld	i,a
		im	1				
		jp	BootSector	; Jump to the boot sector

DMASequence:	db	$C3		; R6: Reset
		db	$79		; R0: DMA transfer PortB->PortA
		dw	BootSector	; destination address
		dw 	$3FF		; size of data buffer
		db	$14		; R1: PortA - memory, incremented, default timming
		db	$28		; R2: PortB - I/O, fixed, default timming
		db	$80		; R3
		db	$8D		; R4: byte mode, no interrupt, no pulse control
		dw	FDD		; source port address (FDD)
		db	$92		; R5: ready active LOW, CE/WAIT, Stop on End-of-Block
		db	$CF		; R6: write settings
		db	$01		; R0: DMA transfer rd:PortB->PortA, wr:PortA->PortB
		db	$CF		; R6: write settings
		db	$87		; R6: enable DMA