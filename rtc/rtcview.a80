;*s
;==============================================================;
;== Verzia 06 == RTC view (clock) == 08.04.1996 == Busy soft ==;
;==============================================================;
	output  "clock.bin"
	
	org	#8000               ;; Bez akehokolvek osetrenia !!!

p:	ei
	ld	a,#07
	call	#3b12               ;; zmazanie obrazovky
loop:	halt
rd2:	ld	hl,buff             ;; |Nacitanie
rd1:	ld	bc,#0503            ;; |aktualneho
	inir	                    ;; |stavu RTC
	ini	                    ;; |do buffera
o:	ld	de,#59a0            ;; Pozicia vypisu na screene
	exx
	ld	hl,buff
	ld	b,#06               ;; slucka pre 6 cislic
loo1:	ld	a,(hl)
	bit	0,b
	exx
	jr	nz,skp1             ;; Ak je parna cislica, urobia este
	inc	e                   ;; medzeru dva atributy - oddelenie
	inc	e                   ;; hodin od minut a minut od sekund
skp1:	and	#0f                 ;; Len spodne 4 bity su platne
	ld	c,a
	add	a,a
	add	a,c
	add	a,low cisla         ;; pozicia cislice
	ld	h,high cisla        ;; v znakovom fonte
	ld	l,a                 ;; Vypis cislice
	ld	b,#03               ;; slucka pre 3 stlpce cislice
loo2:	ld	c,(hl)
	ld	xh,#05              ;; Slucka pre 5 riadkov cislice
	inc	hl
	push	de
loo3:	rrc	c
	ld	a,#2d               ;; farba atributu pre ink
	jr	c,skp2
	xor	a                   ;; farba atributu pre paper
skp2:	ld	(de),a
	ld	a,#e0
	add	a,e
	ld	e,a
	dec	xh
	jr	nz,loo3
	pop	de
	inc	e
	djnz	loo2
	inc	e
	exx
	inc	hl
	djnz	loo1
	xor	a                   ;; test dakej klavesy
	in	a,(#fe)
	or	#e0
	inc	a
	jr	z,loop
	ret	                    ;; a konecne koniec.

cisla:	db	#1f,#11,#1f,#09     ;; Znakovy subor 5x3
	db	#1f,#01,#17,#15
	db	#1d,#11,#15,#1f
	db	#1c,#04,#0f,#1d
	db	#15,#17,#1f,#15
	db	#17,#10,#10,#1f
	db	#1f,#15,#1f,#1d
	db	#15,#1f
k:
l:	equ	k-p
buff:
	ds	#06

	