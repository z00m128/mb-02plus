;*s
;==============================================================;
;== Verzia ** == "hod" Hodinky + set == 06.11.1996 Busy soft ==;
;==============================================================;
;*z!
	device	zxspectrum48
	
verzia	equ	30
rtc	equ	#03                 ;; port
znaky	equ	#6000               ;; adresa sprajtov
font	equ	#7D00               ;; adresa fontu

	org	#5FF0               ;; Druha verzia - s nastavenim
prog	ei
	halt
	res	5,(iy+#01)
	call	zac
	di
	ret	

	include "hodinky-data.a80"

	org	#8000
zac	ld	iy,#5c3a
	ei
	halt
	res	5,(iy+#01)
	ld	hl,23562
	ld	a,(hl)
	ld	(repspd+1),a
	ld	(hl),#05
	ld	bc,#0e00+rtc        ;; Test RTC (ci je installnute)
	ld	e,#50
tstrtc	out	(c),e
	in	a,(c)
	ld	d,a
	and	#0f
	or	#50
	xor	e
	jp	nz,notrtc
	inc	e
	ld	a,e
	and	#0f
	jr	nz,tstrtc

	inc	b                   ;; Naprogramovanie
	ld	a,#04               ;; riadiacich
	out	(c),a               ;; registrov
	dec	b
	ld	a,#06
	out	(c),a
	dec	b
	xor	a
	out	(c),a

	call	screen
	call	show
	call	farby
titul	call	cls
	call	text
	db	#22
	db	'rtc hodinky '
	db	ver1,ver0
	db	' (c) busy soft'
	db	#01
	db	31,9,'stlaCte:',25
	db	's - nastavenie '
	db	'hodIn',8
	db	'space - '
	db	'ukonCenie '
	db	'programu ',0

loop	halt
	call	vypis

	call	inkey
	jr	z,loop
key	cp	#20
	jp	z,basic
	or	#20
	cp	's'
	jr	nz,loop

	call	cls                 ;; Rutinka pre S
	call	text
	db	#22
	db	'naozaj chcete '
	db	'meniT Udaje ??'
	db	#01,31,16
	db	'[a/n]',0

ssyynn	halt
	call	vypis
	call	inkey
	jr	z,ssyynn
	or	#20
	cp	'n'
	jp	z,titul
	cp	'a'
	jr	nz,ssyynn

	call	cls
	call	text
	db	#23
	db	'nastavenie '
	db	'CasovEho '
	db	'Udaja',8,1
	db	'vodorovnE SIpky '
	db	'- vYber',8
	db	'zvislE SIpky - '
	db	'nastavenie',7
	db	'enter - zApis '
	db	'Udaja do rtc',5
	db	'delete - nazad '
	db	'bez zapIsania',0

sstt	call	ukaz
adduu	ld	ix,tabuu
mmloop	ld	l,(ix+#02)
	ld	a,(ix+#03)
	rrca
	rrca
	rrca
	and	#03
	or	#58
	ld	h,a
	ld	(kde+1),hl
	ld	a,(ix+#04)
	ld	(sirka+1),a
ssloop	halt
	ld	a,(23672)
	rrca
	rrca
	rrca
	rrca
	sbc	a,a
	and	#5f-#55
	add	a,#55
kde	ld	hl,#5555
	ld	de,#20
	ld	c,#04
sirka	ld	b,#55
	push	hl
curs1	ld	(hl),a
	inc	l
	djnz	curs1
	pop	hl
	add	hl,de
	dec	c
	jr	nz,sirka
	call	inkey
	jr	z,ssloop
	ld	c,#ff
	cp	10                  ;; dole
	jr	z,nastav
	ld	c,#01
	cp	11                  ;; hore
	jr	z,nastav
	call	farby
	cp	12                  ;; delete
	jp	z,titul
	ld	bc,-6
	cp	8                   ;; vlado
	jr	z,vyber
	ld	bc,6
	cp	9                   ;; vpravo
	jr	z,vyber
	cp	13                  ;; enter
	jr	nz,ssloop
	ld	(adduu+2),ix
	jr	korekt

vyber	add	ix,bc
	ld	a,(ix+#02)
	or	(ix+#03)
	jr	nz,mmloop
	ld	c,(ix+#00)
	ld	b,(ix+#01)
	push	bc
	pop	ix
	jr	mmloop

nastav	ld	l,(ix+#00)
	ld	h,(ix+#01)
	ld	a,(hl)
	add	a,c
	cp	(ix+#05)
	jr	c,nast1
	xor	a
	dec	c
	jr	z,nast1
	ld	a,(ix+#05)
	dec	a
nast1	ld	(hl),a
	call	ukaz
	jp	ssloop

korekt	ld	hl,hod10            ;; Test a skorigovanie
	ld	e,23                ;; casoveho udaju
	call	chkmax              ;; hodiny max. 23

	ld	hl,rok10            ;; Urcenie
	call	getval              ;; priestupneho
	and	#03                 ;; roka
	cp	#01
	sbc	a,a
	and	#01
	or	28                  ;; pocet dni
	ld	(mesdni+1),a        ;; vo februari

	ld	hl,mes10
	call	getval
	or	a
	jr	nz,kkk1
	inc	a
kkk1	cp	13
	jr	c,kk1
	ld	a,12
kk1	call	setval
	call	getval
	dec	a
	add	a,low mesdni
	ld	h,high mesdni
	ld	l,a
	jr	nc,kkk2
	inc	h
kkk2	ld	e,(hl)
	ld	hl,den10
	call	chkmax

zapis	call	write
	jp	titul

chkmax	call	getval              ;; hl=add,e=max
	cp	e
	ret	c
	ret	z
	ld	a,e
setval	ld	(hl),#ff            ;; hl=add,a=val
sst1	inc	(hl)
	sub	10
	jr	nc,sst1
	add	a,10
	inc	hl
	ld	(hl),a
	dec	hl
	ret

getval	ld	a,(hl)              ;; hl=add,a=val
	inc	hl
	ld	c,a
	add	a,a
	add	a,a
	add	a,c
	add	a,a
	add	a,(hl)
	dec	hl
	ret

mesdni	db	31,28,31,30,31,30   ;; dni v mesiacoch
	db	31,31,30,31,30,31

	dw	tabee,0,0
tabuu	dw	hod10,#4000,#0304   ;; add_dat,add_video,limit+sirka
	dw	hod01,#4004,#0a04
	dw	min10,#400c,#0604
	dw	min01,#4010,#0a04
	dw	sec10,#4018,#0604
	dw	sec01,#401c,#0a04
	dw	den10,#4840,#0404
	dw	den01,#4844,#0a04
	dw	mes10,#484c,#0204
	dw	mes01,#4850,#0a04
	dw	rok10,#4858,#0a04
	dw	rok01,#485c,#0a04
tabee	dw	dentt,#40a0,#0720
	dw	tabuu,0,0

inkey	bit	5,(iy+#01)
	ret	z
	res	5,(iy+#01)
	ld	a,#18
	out	(#fe),a
iii	dec	a
	jr	nz,iii
	ld	a,#08
	out	(#fe),a
	ld	a,(23560)
	or	a
	ret

vypis	call	test
	ret	z
show	call	read
ukaz	ld	hl,#4018            ;; cas
	ld	(zncur+1),hl
	ld	hl,sec10
	call	cislo
	xor	a
	ld	(zncur+1),a
	ld	hl,hod10
	call	cislo
	ld	a,':'
	call	znak
	ld	hl,min10
	call	cislo
	ld	a,':'
	call	znak

	ld	a,#a0               ;; den v tyzdni
	ld	(zncur+1),a
	ld	a,(dentt)
	cp	#07
	jr	c,uuu
	xor	a
uuu	add	a,a
	add	a,a
	add	a,a
	add	a,low dni
	ld	h,high dni
	ld	l,a
	jr	nc,uuuu
	inc	h
uuuu	ld	b,#08
	ld	a,(hl)
	cp	'.'
	jr	nz,ddnxt
	dec	b
	inc	hl
	ld	a,#a2
	ld	(zncur+1),a
ddnxt	ld	a,(hl)
	call	znak
	inc	hl
	djnz	ddnxt

	ld	hl,#4840            ;; datum
	ld	(zncur+1),hl
	ld	hl,den10
	call	cislo
	ld	a,#3d
	call	znak
	ld	hl,mes10
	call	cislo
	ld	a,#3d
	call	znak
	ld	hl,rok10
	jp	cislo

farby	ld	hl,#5800            ;; farby
	ld	de,#5801
	ld	(hl),#07
	ld	bc,#80
	ldir
	ld	(hl),#04
	ld	c,#c0
	ldir
	ld	(hl),#05
	ld	c,#80
	ldir
	ret

test	ld	bc,#0d00+rtc        ;; Test ci sa uz
	in	e,(c)               ;; zmenil casovy udaj
	bit	2,e
	ret	z
	ld	e,#02
	out	(c),e
	ret

write	ld	bc,#0f00+rtc        ;; Zapisanie casu do RTC
	ld	a,#05
	out	(c),a
	ld	hl,bufrtc
	ld	bc,#0c00+rtc
ooo	ld	a,(hl)
	out	(c),a
	inc	hl
	dec	b
	jp	p,ooo
	ld	a,#04
	ld	b,#0f
	out	(c),a
	ret

read	ld	xh,#10              ;; Precitanie casu z RTC
rtcrd1	ld	hl,bufrtc
	ld	bc,#0d00+rtc
	xor	a
	out	(c),a
	dec	b
	inir
	ini
	ld	b,#0d
	in	a,(c)
	bit	2,a
	jr	z,readok
	dec	xh
	jr	nz,rtcrd1
readok	ld	hl,bufrtc
	ld	bc,#0d
rtcmsk	ld	a,#0f
	and	(hl)
	ld	(hl),a
	cpi
	jp	pe,rtcmsk
	ret

cislo	call	cis1
cis1	ld	a,(hl)
	inc	hl
	or	'0'
	cp	'9'+1
	jr	c,znak
	ld	a,'?'
znak	push	hl
	push	de
	push	bc
	cp	' '
	jr	nz,ccc
	ld	a,'@'
ccc	sub	#30
	ld	c,a
	and	#07
	add	a,a
	add	a,a
	ld	l,a
	ld	a,c
	and	#08
	rrca
	rrca
	rrca
	rrca
	or	l
	ld	l,a
	ld	a,c
	and	#30
	rrca
	add	a,high znaky
	ld	h,a
	ld	c,#04
znk2	ld	b,#04
zncur	ld	de,#4000
	push	hl
znk1	ld	a,(hl)
	ld	(de),a
	inc	h
	inc	d
	dw	#127e,#1424
	dw	#127e,#1424
	dw	#127e,#1424
	dw	#127e,#1424
	dw	#127e,#1424
	dw	#127e,#1424
	dw	#127e,#1424
	ld	a,#20
	add	a,l
	ld	l,a
	jr	c,zndd1
	ld	a,#f8
	add	a,h
	ld	h,a
zndd1	ld	a,#20
	add	a,e
	ld	e,a
	jr	c,zndd2
	ld	a,#f8
	add	a,d
	ld	d,a
zndd2	djnz	znk1
	ld	hl,zncur+1
	inc	(hl)
	pop	hl
	inc	l
	dec	c
	jr	nz,znk2
	pop	bc
	pop	de
	pop	hl
	ret

screen	halt
	ld	a,#08
	out	(#fe),a
	ld	hl,#4000
	ld	de,#4001
	ld	bc,#19e0
	ld	(hl),l
	ldir
	ld	(hl),#12
	ld	c,#21
	ldir
	ld	(hl),#06
	ld	c,#1e
	ldir
	ld	(hl),#12
	ld	c,#e0
	ldir
	ld	hl,#5a00
	ld	de,#5a20
	ld	c,#c0
	ldir
	ret

cls	ld	hl,#5000
	ld	de,#5001
	ld	bc,#07ff
	ld	(hl),l
	ldir
	ret

disp	push	hl                  ;; jeden znak 8x8 pixelov
	push	de
	push	bc
	add	a,a
	ld	l,a
	ld	h,#1f
	add	hl,hl
	add	hl,hl
cursor	ld	de,#5055
	ld	b,#08
prn2	ld	a,(hl)
tucne	rra
	or	(hl)
	ld	(de),a
	inc	hl
	inc	d
	djnz	prn2
	inc	e
	ld	a,e
	ld	(cursor+1),a
	pop	bc
	pop	de
	pop	hl
	ret

text	pop	hl
	ld	a,(hl)
	ld	(cursor+1),a
	ld	a,#1f
	ld	(tucne),a
txt1	inc	hl
	ld	a,(hl)
	or	a                   ;; #00 = koniec textu
	jr	z,txtret
	cp	#03
	jr	c,txttuc            ;; #01,#02 = chude/tucne pismo
	cp	#20
	jr	c,txttab            ;; #03..#1f = multimedzery
	call	disp
	jr	txt1                ;; #20..#ff = znaky
txtret	jp	(hl)
txttab	ld	de,cursor+1
	ld	a,(de)
	add	a,(hl)
	ld	(de),a
	jr	txt1
txttuc	rrca
	rrca
	sbc	a,a
	and	#1f
	ld	(tucne),a
	jr	txt1

notrtc	call	screen
	call	text
	db	#22
	db	'v tejto '
	db	'konfigurAcii '
	db	'nie je ',4
	db	'inStalovanY '
	db	'rtc koprocesor!!'
	db	#01
	db	31,10,'stlaCte '
	db	'klAvesu...',0
cakaj	halt
	call	inkey
	jr	z,cakaj
basic
repspd	ld	a,#55
	ld	(23562),a
	ld	hl,#4000
	ld	de,#4001
	ld	bc,#1800
	ld	(hl),l
	ldir
	halt
	ld	a,(23624)
	rrca
	rrca
	rrca
	and	#07
	or	#08
	out	(#fe),a
	ld	a,(23693)
	ld	(hl),a
	ld	bc,#02ff
	ldir
	ret

dni	db	' NEDELA '          ;; 0
	db	'PONDELOK'          ;; 1
	db	' UTOROK '          ;; 2
	db	' STREDA '          ;; 3
	db	'.STVRTOK'          ;; 4
	db	' PIATOK '          ;; 5
	db	' SOBOTA '          ;; 6

kon
len	equ	kon-prog
vera	equ	verzia/10
verb	equ	vera*10
verc	equ	verzia-verb
ver0	equ	'0'+verc
ver1	equ	'0'+vera

bufrtc	ds	#0d
	nop

sec01	equ	bufrtc+#0c
sec10	equ	bufrtc+#0b
min01	equ	bufrtc+#0a
min10	equ	bufrtc+#09
hod01	equ	bufrtc+#08
hod10	equ	bufrtc+#07
den01	equ	bufrtc+#06
den10	equ	bufrtc+#05
mes01	equ	bufrtc+#04
mes10	equ	bufrtc+#03
rok01	equ	bufrtc+#02
rok10	equ	bufrtc+#01
dentt	equ	bufrtc+#00



	savebin "hodinky.bin",prog,$-prog
	